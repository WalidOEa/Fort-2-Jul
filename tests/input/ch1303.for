      PROGRAM EASTER
      INTEGER YEAR,METCYC,CENTRY,ERROR1,ERROR2,DAY
      INTEGER EPACT,LUNA
C A PROGRAM TO CALCULATE THE DATE OF EASTER
      PRINT *,' INPUT THE YEAR FOR WHICH EASTER'
      PRINT *,' IS TO BE CALCULATED'
      PRINT *,' ENTER THE WHOLE YEAR, E.G. 1978 '
      READ *,YEAR
C CALCULATING THE YEAR IN THE 19 YEAR METONIC CYCLE-METCYC
      METCYC = MOD(YEAR,19)+1
      IF(YEAR.LE.1582)THEN
        DAY = (5*YEAR)/4
        EPACT = MOD(11*METCYC-4,30)+1
      ELSE
C CALCULATING THE CENTURY-CENTRY
      CENTRY = (YEAR/100)+1
C ACCOUNTING FOR ARITHMETIC INACCURACIES
C IGNORES LEAP YEARS ETC.
        ERROR1 = (3*CENTRY/4)-12
        ERROR2 = ((8*CENTRY+5)/25)-5
C LOCATING SUNDAY
        DAY = (5*YEAR/4)-ERROR1-10
C LOCATING THE EPACT(FULL MOON)
        EPACT = MOD(11*METCYC+20+ERROR2-ERROR1,30)
        IF(EPACT.LT.0)EPACT=30+EPACT
        IF((EPACT.EQ.25.AND.METCYC.GT.11).OR.EPACT.EQ.24)THEN
          EPACT=EPACT+1
        ENDIF
      ENDIF
C FINDING THE FULL MOON
      LUNA=44-EPACT
      IF(LUNA.LT.21)THEN
        LUNA=LUNA+30
      ENDIF
C LOCATING EASTER SUNDAY
      LUNA=LUNA+7-(MOD(DAY+LUNA,7))
C LOCATING THE CORRECT MONTH
      IF(LUNA.GT.31)THEN
        LUNA = LUNA - 31
        PRINT *,' FOR THE YEAR ',YEAR
        PRINT *,' EASTER FALLS ON APRIL ',LUNA
      ELSE
        PRINT *,' FOR THE YEAR ',YEAR
        PRINT *,' EASTER FALLS ON MARCH ',LUNA
      ENDIF
      END